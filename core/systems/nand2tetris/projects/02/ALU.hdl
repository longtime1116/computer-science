// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Not16 (in=x, out=notx);
    And16 (a=x, b=notx, out=zeros);

    // zx
    Mux16 (a=x, b=zeros, sel=zx, out=xafterzx);
    // nx
    Not16 (in=xafterzx, out=notxafterzx);
    Mux16 (a=xafterzx, b=notxafterzx, sel=nx, out=xafternx);

    // zy
    Mux16 (a=y, b=zeros, sel=zy, out=yafterzy);
    // ny
    Not16 (in=yafterzy, out=notyafterzy);
    Mux16 (a=yafterzy, b=notyafterzy, sel=ny, out=yafterny);

    Add16 (a=xafternx, b=yafterny, out=xplusy);
    And16 (a=xafternx, b=yafterny, out=xandy);

    // f
    Mux16 (a=xandy, b=xplusy, sel=f, out=xyafterf);

    // no
    Not16 (in=xyafterf, out=notxyafterf);
    Mux16 (a=xyafterf, b=notxyafterf, sel=no, out=xyafterno);


    // zr
    And16 (a=xyafterno, b=xyafterno, out[0..7]=former, out[8..15]=latter);
    Or8Way (in=former, out=or1);
    Or8Way (in=latter, out=or2);
    Or (a=or1, b=or2, out=notzr);
    Not (in=notzr, out=zr);

    // ng
    And16 (a=xyafterno, b=xyafterno, out[0..14]=head, out[15]=last);
    And (a=last, b=true, out=ng);

    // out
    And16 (a=xyafterno, b=xyafterno, out=out);
}
